#!/usr/bin/gnuplot
reset;
set encoding utf8;

CPM2DRE=1.0/334.0;
NOW=system("TZ=" . CONFIG_TIMEZONE . " date +%FT%T" . CONFIG_TZ);
DATAFILE=sprintf("cache/%d.csv", ID);
OUTFILE=sprintf("out/%d.png", ID);
OUTFILE_small=sprintf("out/%d_small.png", ID);
TABLE=sprintf("tmp/%d.data", ID);

# This converts the time string in column COL to a number of seconds
iso2s(COL) = strptime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, strcol(COL));
s2iso(seconds) = strftime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, seconds);

set datafile separator ",";
set key autotitle columnheader top right;
set grid front;
S_START=strptime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, system("TZ=" . CONFIG_TIMEZONE . " date +%FT%T" . CONFIG_TZ . " --date='" . PERIOD_START . "'"));
S_END=strptime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, NOW);
set xrange [ S_START : S_END ]; # set xlabel "timestamp";
stats DATAFILE u (iso2s(1)):2 nooutput;

min_y = (1.1*STATS_min_y < STATS_mean_y - 4*STATS_stddev_y) ? 1.1*STATS_min_y : STATS_mean_y - 4*STATS_stddev_y; min_y = (min_y > 0) ? min_y : 0;
max_y = (1.1*STATS_max_y > STATS_mean_y + 4*STATS_stddev_y) ? 1.1*STATS_max_y : STATS_mean_y + 4*STATS_stddev_y;
#min_y = 0; max_y = 350;
set yrange [min_y: max_y]; set ylabel "CPM";
set y2range [min_y * CPM2DRE: max_y * CPM2DRE]; set y2tics nomirror textcolor rgb "blue"; set y2label "μSv/h" textcolor rgb "blue";
set arrow 2 nohead front from graph 1, graph 0 to graph 1, graph 1 lc rgb "blue";

set xdata time; set timefmt "%Y-%m-%dT%H:%M:%S" . CONFIG_TZ; set format x "%Y-%m-%d\n%H:%M " . CONFIG_TZ;

sma_24_init(x) = (k24=k23=k22=k21=k20=k19=k18=k17=k16=k15=k14=k13=k12=k11=k10=k9=k8=k7=k6=k5=k4=k3=k2=k1=x);
sma_24_shift(x) = (k24=k23, k23=k22, k22=k21, k21=k20, k20=k19, k19=k18, k18=k17, k17=k16, K16=k15, K15=k14, k14=k13, k13=k12, k12=k11, k11=k10, k10=k9, k9=k8, k8=k7, k7=k6, k6=k5, k5=k4, k4=k3, k3=k2, k2=k1, k1 = x);
sma_24(x) = (sma_24_shift(x), (k24+k23+k22+k21+k20+k19+k18+k17+k16+k15+k14+k13+k12+k11+k10+k9+k8+k7+k6+k5+k4+k3+k2+k1)/24);

set ytics add ("mean" STATS_mean_y);
#set y2tics add ("mean" STATS_mean_y * CPM2DRE);
set ytics add ("+3σ    " STATS_mean_y + 3*STATS_stddev_y);
set ytics add ("-3σ    " STATS_mean_y - 3*STATS_stddev_y);

set object 1 rectangle from "2013-01-01", STATS_mean_y - 3*STATS_stddev_y to "2015-01-01", STATS_mean_y + 3*STATS_stddev_y back fc rgb "cyan" fillstyle solid 0.1;
set title sprintf("sensor=%d, total %d records, %0.1f±%0.1f CPM (%0.2f±%0.2f μSv/h)", ID, STATS_records, STATS_mean_y, 3*STATS_stddev_y, STATS_mean_y * CPM2DRE , 3*STATS_stddev_y * CPM2DRE);

set term png enhanced font "Arial Unicode MS,8" size CONFIG_WIDTH_BIG, CONFIG_HEIGHT_BIG background "#ffffef";
set output OUTFILE;

set style line 1 lc rgb "orange";
plot \
	DATAFILE u 1:2 w p ls 1 notitle, \
	a = sma_24_init(STATS_mean_y), \
	DATAFILE u 1:(sma_24($2)) w l lw 2 title "CPM, 24-bin SMA";

# plot small PNG for Fusion tables
unset object 1;
set term png enhanced font "Arial Unicode MS,7" size CONFIG_WIDTH_SMALL, CONFIG_HEIGHT_SMALL background "#ffffff";
set output OUTFILE_small;
uSv_h = sprintf("%0.2f±%0.2f μSv/h", STATS_mean_y * CPM2DRE , 3*STATS_stddev_y * CPM2DRE);
set title sprintf("%0.1f±%0.1f CPM (%s)", STATS_mean_y, 3*STATS_stddev_y, uSv_h) font "Arial Unicode MS,9";
unset ytics; set ytics;
set yrange [0: 350];
unset y2tics; unset y2label;
set format x "%Y-%m-%d";
plot \
	a = sma_24_init(STATS_mean_y), \
	DATAFILE u 1:(sma_24($2)) w l ls 1 notitle;

set format x "%Y-%m-%dT%H:%M:%S" . CONFIG_TZ;
set table TABLE;
plot a = sma_24_init(STATS_mean_y), DATAFILE u 1:(sma_24($2)) title sprintf("%d: %s", ID, uSv_h);
unset table;
