#!/usr/bin/gnuplot
reset;
set encoding utf8;

NOW=system("TZ=" . CONFIG_TIMEZONE . " date +%FT%T" . CONFIG_TZ);
#set key maxrows 4 width 1.0 height 0.8 spacing 0.8 box top center;
set lmargin at screen 0.25;
set key at screen 0.23, graph 1.0 Right opaque samplen 1.5 spacing 1.0 height 0.9 title "      ID: μSv/h ± 3σ [CPM2DRE]" noenhanced nobox maxcols 1;
set grid x y2 front; 
S_START=strptime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, PERIOD_START);
S_END=strptime("%Y-%m-%dT%H:%M:%S" . CONFIG_TZ, PERIOD_END);
set xrange [ S_START : S_END ]; # set xlabel "timestamp";

unset ytics;
set y2range [0.0 : 1.2]; set y2tics nomirror textcolor rgb "blue"; set y2label "μSv/h" textcolor rgb "blue";
set arrow 2 nohead front from graph 1, graph 0 to graph 1, graph 1 lc rgb "blue";

set xdata time; set timefmt '"%Y-%m-%dT%H:%M:%S' . CONFIG_TZ . '"'; set format x "%Y-%m-%d\n%H:%M " . CONFIG_TZ;

sma_24_init(x) = (w24=w23=w22=w21=w20=w19=w18=w17=w16=w15=w14=w13=w12=w11=w10=w9=w8=w7=w6=w5=w4=w3=w2=w1=x);
sma_24_shift(x) = (w24=w23,w23=w22,w22=w21,w21=w20,w20=w19,w19=w18,w18=w17,w17=w16,w16=w15,w15=w14,w14=w13,w13=w12,w12=w11,w11=w10,w10=w9,w9=w8,w8=w7,w7=w6,w6=w5,w5=w4,w4=w3,w3=w2,w2=w1,w1=x);
sma_24_samples(x) = $0 > 23 ? 24 : ($0+1);
sma_24_RO(x) = (w24+w23+w22+w21+w20+w19+w18+w17+w16+w15+w14+w13+w12+w11+w10+w9+w8+w7+w6+w5+w4+w3+w2+w1)/sma_24_samples($0);
sma_24(x) = (sma_24_shift(x), sma_24_RO(x));

set term png enhanced font "Arial Unicode MS,10" size CONFIG_WIDTH_ALL, CONFIG_HEIGHT_ALL background "#ffffef";
set output OUTFILE;
set title "Safecast fixed sensors: DRE at " . NOW;

graph_title(n) = system("cat tmp/" . n . ".title");
plot for [ID in IDs] w = sma_24_init(0) "tmp/" . ID . ".data" u 1:(sma_24($2)) axes x1y2 w l lw 2 title graph_title(ID);
